#version 430 core

layout(binding = 2, rgba32f) uniform image2D framebuffer;       
layout(binding = 3, rgba32f) uniform image2D sqAvgBuffer;       


uniform sampler2D colorTexture;
uniform mat4 mat_inverse;
uniform mat4 persp_inverse;
uniform float lightIntensity;
uniform vec3 lightPosition;
uniform bool blinnPhong;
uniform bool transparent;
uniform float shininess;
uniform float eta;
uniform float radius;
uniform float groundDistance;
uniform vec3 center;
uniform int framecount;

float halton[256] = {0.023809523809523808, 0.047619047619047616, 0.07142857142857142, 0.09523809523809523, 0.11904761904761904, 0.14285714285714285, 0.16666666666666666, 0.19047619047619047, 0.21428571428571427, 0.23809523809523808, 0.26190476190476186, 0.2857142857142857, 0.30952380952380953, 0.3333333333333333, 0.3571428571428571, 0.38095238095238093, 0.40476190476190477, 0.42857142857142855, 0.45238095238095233, 0.47619047619047616, 0.5, 0.5238095238095237, 0.5476190476190476, 0.5714285714285714, 0.5952380952380952, 0.6190476190476191, 0.6428571428571428, 0.6666666666666666, 0.6904761904761905, 0.7142857142857142, 0.738095238095238, 0.7619047619047619, 0.7857142857142857, 0.8095238095238095, 0.8333333333333333, 0.8571428571428571, 0.8809523809523809, 0.9047619047619047, 0.9285714285714285, 0.9523809523809523, 0.9761904761904762, 0.0005668934240362812, 0.02437641723356009, 0.0481859410430839, 0.0719954648526077, 0.09580498866213151, 0.11961451247165532, 0.14342403628117914, 0.16723356009070295, 0.19104308390022676, 0.21485260770975056, 0.23866213151927437, 0.2624716553287981, 0.28628117913832196, 0.3100907029478458, 0.3339002267573696, 0.35770975056689336, 0.3815192743764172, 0.40532879818594103, 0.4291383219954648, 0.4529478458049886, 0.4767573696145124, 0.5005668934240363, 0.52437641723356, 0.5481859410430838, 0.5719954648526077, 0.5958049886621315, 0.6196145124716553, 0.6434240362811791, 0.6672335600907029, 0.6910430839002267, 0.7148526077097505, 0.7386621315192743, 0.7624716553287981, 0.786281179138322, 0.8100907029478458, 0.8339002267573695, 0.8577097505668934, 0.8815192743764172, 0.9053287981859409, 0.9291383219954648, 0.9529478458049886, 0.9767573696145124, 0.0011337868480725624, 0.02494331065759637, 0.04875283446712018, 0.07256235827664399, 0.0963718820861678, 0.12018140589569161, 0.1439909297052154, 0.1678004535147392, 0.19160997732426302, 0.21541950113378683, 0.23922902494331064, 0.26303854875283444, 0.2868480725623583, 0.3106575963718821, 0.3344671201814059, 0.3582766439909297, 0.3820861678004535, 0.40589569160997735, 0.42970521541950113, 0.4535147392290249, 0.47732426303854875, 0.5011337868480725, 0.5249433106575963, 0.5487528344671201, 0.5725623582766439, 0.5963718820861678, 0.6201814058956916, 0.6439909297052153, 0.6678004535147392, 0.691609977324263, 0.7154195011337867, 0.7392290249433106, 0.7630385487528344, 0.7868480725623582, 0.8106575963718821, 0.8344671201814058, 0.8582766439909296, 0.8820861678004535, 0.9058956916099772, 0.929705215419501, 0.9535147392290249, 0.9773242630385487, 0.0017006802721088437, 0.025510204081632654, 0.04931972789115646, 0.07312925170068027, 0.09693877551020408, 0.12074829931972789, 0.1445578231292517, 0.1683673469387755, 0.1921768707482993, 0.21598639455782312, 0.23979591836734693, 0.2636054421768707, 0.28741496598639454, 0.3112244897959184, 0.33503401360544216, 0.35884353741496594, 0.3826530612244898, 0.4064625850340136, 0.4302721088435374, 0.4540816326530612, 0.477891156462585, 0.5017006802721088, 0.5255102040816325, 0.5493197278911564, 0.5731292517006802, 0.596938775510204, 0.6207482993197279, 0.6445578231292516, 0.6683673469387754, 0.6921768707482993, 0.715986394557823, 0.7397959183673468, 0.7636054421768707, 0.7874149659863945, 0.8112244897959183, 0.835034013605442, 0.8588435374149659, 0.8826530612244897, 0.9064625850340134, 0.9302721088435373, 0.9540816326530611, 0.977891156462585, 0.0022675736961451248, 0.026077097505668934, 0.04988662131519274, 0.07369614512471655, 0.09750566893424036, 0.12131519274376416, 0.14512471655328799, 0.1689342403628118, 0.1927437641723356, 0.2165532879818594, 0.24036281179138322, 0.26417233560090697, 0.2879818594104308, 0.31179138321995464, 0.3356009070294784, 0.3594104308390022, 0.38321995464852604, 0.4070294784580499, 0.43083900226757366, 0.45464852607709744, 0.4784580498866213, 0.5022675736961452, 0.5260770975056689, 0.5498866213151927, 0.5736961451247166, 0.5975056689342404, 0.6213151927437642, 0.645124716553288, 0.6689342403628118, 0.6927437641723356, 0.7165532879818594, 0.7403628117913832, 0.764172335600907, 0.7879818594104309, 0.8117913832199547, 0.8356009070294784, 0.8594104308390023, 0.8832199546485261, 0.9070294784580498, 0.9308390022675737, 0.9546485260770975, 0.9784580498866213, 0.002834467120181406, 0.026643990929705215, 0.05045351473922902, 0.07426303854875282, 0.09807256235827663, 0.12188208616780044, 0.14569160997732425, 0.16950113378684806, 0.19331065759637187, 0.21712018140589567, 0.24092970521541948, 0.2647392290249433, 0.2885487528344671, 0.31235827664399096, 0.33616780045351474, 0.3599773242630385, 0.38378684807256236, 0.4075963718820862, 0.43140589569161, 0.45521541950113376, 0.4790249433106576, 0.5028344671201814, 0.5266439909297052, 0.550453514739229, 0.5742630385487528, 0.5980725623582767, 0.6218820861678005, 0.6456916099773242, 0.6695011337868481, 0.6933106575963719, 0.7171201814058956, 0.7409297052154195, 0.7647392290249433, 0.7885487528344671, 0.812358276643991, 0.8361678004535147, 0.8599773242630385, 0.8837868480725624, 0.9075963718820861, 0.9314058956916099, 0.9552154195011338, 0.9790249433106576, 0.0034013605442176874, 0.027210884353741496, 0.05102040816326531, 0.07482993197278912, 0.09863945578231292};

#define MAX_SCENE_BOUNDS    10.0
#define EPS                 0.000001

#define MIN_DISTANCE EPS
#define GROUND_VPTR -1

#define M_1_PI 0.31830988618

#define Ka 0.5
#define Kd 0.3

float PHI = 1.61803398874989484820459;  // Φ = Golden Ratio   

float gold_noise(in vec2 xy, in float seed){
       return fract(tan(distance(xy*PHI, xy)*seed)*xy.x);
}

struct hitinfo_t {
    /* Index of the first index of the triangle */
    /* or: GROUND_VPTR */
    int hit_vptr;
    /* Intersection distance, barycentric coordinates */
    /* or: intersection distance, 0, texture coordinates (for the ground) */
    vec4 t;
};

struct BVHNode {
    vec4 min;
    vec4 max;
    int nbTriangles;
    int index;
};

layout (std430, binding = 1) buffer Vertices
{
    vec4 vertices[];
};

layout (std430, binding = 2) buffer Normals
{
    vec4 normals[];
};

layout (std430, binding = 3) buffer Colors
{
    vec4 colors[];
};

layout (std430, binding = 4) buffer Meshes
{
    int indices[];
};

layout (std430, binding = 5) buffer BVHNodes
{
    BVHNode nodes[];
};


// Check whether there is an intersection between the ray (origin, dir) and the bounding box (bbmin, bbmax), 
// stores into tNear the nearest point of intersection on the ray.
bool intersectBoundingBox(vec4 origin, vec4 dir, vec4 bbmin, vec4 bbmax, out float tNear)
{
	// intersect with BB:
    vec4 sMin = (bbmin - origin) / dir;
    vec4 sMax = (bbmax - origin) / dir;

    vec4 tMin = min(sMin, sMax);
    vec4 tMax = max(sMin, sMax);

    tNear = max(max(tMin.x, tMin.y), tMin.z);
    float tFar = min(min(tMax.x, tMax.y), tMax.z);

	if (tNear < 0) tNear = 0; // starting point inside the BVH
    return tNear <= tFar;
}

// intersect ray (origin, dir) with triangle defined by ptr (number inside indexed face set)
// returns a boolean, plus the "t" of hitinfo structure (Intersection distance, barycentric coordinates)
bool intersectTriangle(vec4 origin, vec4 dir, int ptr, out vec4 dist)
{
	// vertices from data:
    vec4 v0 = vertices[indices[ptr]];
    vec4 v1 = vertices[indices[ptr + 1]];
    vec4 v2 = vertices[indices[ptr + 2]];

    vec3 e1 = (v1 - v0).xyz;
    vec3 e2 = (v2 - v0).xyz;

    vec3 p = cross(dir.xyz, e2);
    float det = dot(e1, p);
    if (abs(det) < EPS) return false;

    // We do early exit here: as soon as we can return false, we do.
    // In my tests, it is faster than computing all coordinates, 
    // *then* checking validity with a single test. YMMV, feel free to test
    vec3 t = (origin - v0).xyz;
    dist.y = dot(t, p);
    if (dist.y < 0.0 || dist.y > det) return false;
    vec3 q = cross(t, e1);
    dist.z = dot(dir.xyz, q);
    if (dist.z < 0.0 || dist.z > det) return false;
    dist.w = det - dist.y - dist.z;
    if (dist.w < 0.0) return false;
    dist.x = dot(e2, q);
    dist /= det;
    return (dist.x > EPS);
}

bool intersectGround(vec4 origin, vec4 dir, out vec4 dist)
{
    if (abs(dir.y) < EPS)
        return false;
    float t = -(origin.y + groundDistance) / dir.y;

    if (t < MIN_DISTANCE)
        return false;

    vec4 point = origin + t * dir;
    point.y = -groundDistance;
    vec4 scaledPoint = point / radius ;
    vec2 texCoords = 0.5 * scaledPoint.xz + 0.5;

    dist = vec4(t, 0.0, texCoords);

    return true;
}

vec4 interpolateVertex(hitinfo_t h)
{
    if (vertices.length() > 0) {
		vec4 v0 = vertices[indices[h.hit_vptr]];
		vec4 v1 = vertices[indices[h.hit_vptr + 1]];
		vec4 v2 = vertices[indices[h.hit_vptr + 2]];
		return normalize(h.t.w * v0 + h.t.y * v1 + h.t.z * v2);
	} else return vec4(0.0, 0.0, 0.0, 1.0); // should not be even possible ?
}

vec4 interpolateColor(hitinfo_t h)
{
	if (colors.length() > 0) {
		vec4 c0 = colors[indices[h.hit_vptr]];
		vec4 c1 = colors[indices[h.hit_vptr + 1]];
		vec4 c2 = colors[indices[h.hit_vptr + 2]];
		return (h.t.w * c0 + h.t.y * c1 + h.t.z * c2);
	} else return vec4(0.7, 0.7, 0.7, 1.0);
}

vec4 interpolateNormal(hitinfo_t h)
{
    if (normals.length() > 0) {
		vec4 n0 = normals[indices[h.hit_vptr]];
		vec4 n1 = normals[indices[h.hit_vptr + 1]];
		vec4 n2 = normals[indices[h.hit_vptr + 2]];
		return normalize(h.t.w * n0 + h.t.y * n1 + h.t.z * n2);
	} else return vec4(0.5, 0.5, 0.5, 1.0);
}

// BVH-related functions:
bool isLeaf(BVHNode node)
{
    return node.nbTriangles != 0;
}

bool intersectBVHLeaf(BVHNode node, vec4 origin, vec4 dir, inout hitinfo_t h)
{
    bool hit = false;

    for (int i = 0; i < node.nbTriangles; ++i) {
        int vptr = 3 * (node.index + i);
        vec4 dist;
        if (intersectTriangle(origin, dir, vptr, dist) && dist.x < h.t.x) {
            hit = true;
            h.hit_vptr = vptr;
            h.t = dist;
        }
    }

    return hit;
}

bool intersectBVHLeaf(BVHNode node, vec4 origin, vec4 dir)
{
    for (int i = 0; i < node.nbTriangles; ++i) {
        int vptr = 3 * (node.index + i);
        vec4 dist;
        if (intersectTriangle(origin, dir, vptr, dist))
            return true;
    }

    return false;
}

#define MAX_STACK_SIZE 64

#define push(stack, size, value) stack[size++] = value
#define pop(stack, size) stack[--size]

bool intersectBVH(vec4 origin, vec4 dir, inout hitinfo_t h)
{
    int rootIndex = 0;
    BVHNode rootNode = nodes[rootIndex];
    float tNear;

    if (!intersectBoundingBox(origin, dir, rootNode.min, rootNode.max, tNear))
        return false;

    struct StackElement {
        int index;
        float t;
    } stack[MAX_STACK_SIZE];
    int size = 0;

    push(stack, size, StackElement(rootIndex, 0.0));

    bool hit = false;

    while (size != 0) {
        StackElement current = pop(stack, size);
        if (h.t.x < current.t)
            continue;

        BVHNode currentNode = nodes[current.index];
        if (isLeaf(currentNode)) {
            hit = intersectBVHLeaf(currentNode, origin, dir, h) || hit;
        } else {
            StackElement left = StackElement(current.index + 1, 0.0);
            BVHNode leftNode = nodes[left.index];
            bool hitLeft = intersectBoundingBox(origin, dir, leftNode.min, leftNode.max, left.t);

            StackElement right = StackElement(currentNode.index, 0.0);
            BVHNode rightNode = nodes[right.index];
            bool hitRight = intersectBoundingBox(origin, dir, rightNode.min, rightNode.max, right.t);

            if (hitLeft && hitRight) {
                if (left.t < right.t) {
                    push(stack, size, right);
                    push(stack, size, left);
                } else {
                    push(stack, size, left);
                    push(stack, size, right);
                }
            } else if (hitLeft) {
                push(stack, size, left);
            } else if (hitRight) {
                push(stack, size, right);
            }
        }
    }

    return hit;
}

bool intersectBVH(vec4 origin, vec4 dir)
{
    int rootIndex = 0;
    BVHNode rootNode = nodes[rootIndex];
    float tNear;

    if (!intersectBoundingBox(origin, dir, rootNode.min, rootNode.max, tNear))
        return false;

    int stack[MAX_STACK_SIZE];
    int size = 0;

    push(stack, size, rootIndex);

    while (size != 0) {
        int current = pop(stack, size);
        BVHNode currentNode = nodes[current];
        if (isLeaf(currentNode)) {
            if (intersectBVHLeaf(currentNode, origin, dir))
                return true;
        } else {
            int left = current + 1;
            BVHNode leftNode = nodes[left];
            bool hitLeft = intersectBoundingBox(origin, dir, leftNode.min, leftNode.max, tNear);

            int right = currentNode.index;
            BVHNode rightNode = nodes[right];
            bool hitRight = intersectBoundingBox(origin, dir, rightNode.min, rightNode.max, tNear);

            if (hitLeft && hitRight) {
                push(stack, size, right);
                push(stack, size, left);
            } else if (hitLeft) {
                push(stack, size, left);
            } else if (hitRight) {
                push(stack, size, right);
            }
        }
    }

    return false;
}
// End BVH-related functions

// Compute the first intersection along the ray (origin, dir). 
// returns the nearest intersection in h
bool firstIntersection(vec4 origin, vec4 dir, out hitinfo_t h)
{
    h.t.x = radius * MAX_SCENE_BOUNDS;

    bool hit = intersectBVH(origin, dir, h);

    vec4 dist;
    if (intersectGround(origin, dir, dist) && dist.x < h.t.x) {
        hit = true;
        h.hit_vptr = GROUND_VPTR;
        h.t = dist;
    }

	return hit;
}

// checks whether there is any intersection along the ray (origin, dir)
// Does not compute the nearest intersection, ends the loop as soon as an intersection is found.
bool isIntersected(vec4 origin, vec4 dir)
{
    vec4 dist;

    return intersectGround(origin, dir, dist) || intersectBVH(origin, dir);
}

// Local illumination related functions. TODO

/*
The key function is directIllumination(). It is called by trace() for each pixel of the screen that covers an object of the scene (we'll cover this in the ray-tracing lecture).

First, edit this function so that it computes local illumination using the Blinn-Phong model. Make it as modular as possible, so you can reuse code in future works, but also move code aside when you change lighting model.

You already have the point position, its color and the incoming viewing direction ​V​⃗​​. You will have to compute the shading normal ​n​⃗​​ and the vector to the light source ​L​⃗​​. 
*/

vec4 lightDirection(vec4 p)
{
    return normalize(vec4(lightPosition, 1) - p);
}

bool inShadow(vec4 p, vec4 n)
{
    vec4 lightDir = lightDirection(p);

    // Positive half-space of shading normal
    bool positiveHalfSpace = dot(n, lightDir) > 0;

    if (!positiveHalfSpace)
        return true;

    // No intersections toward light-source
    bool lightObstructed = isIntersected(p, lightDir); 

    return lightObstructed;
}

vec4 blinnPhongAmbient(vec4 color)
{
    return color * Ka * lightIntensity;
}

vec4 blinnPhongDiffuse(vec4 color, vec4 normal, vec4 lightDirection)
{
    return Kd * color * max(dot(normal, lightDirection), 0) * lightIntensity;
}

float fresnel(float cosTheta)
{
    float sinThetaSqrd = 1 - pow(cosTheta, 2);

    float ci = sqrt(pow(eta, 2) - sinThetaSqrd);

    float Fs = pow(abs((cosTheta - ci) / (cosTheta + ci)), 2);
    float Fp = pow(abs((pow(eta, 2) * cosTheta - ci) / (pow(eta, 2) * cosTheta + ci)), 2);  
    
    return (Fs + Fp) / 2;
}

vec4 halfVector(vec4 a, vec4 b)
{
    return normalize(a + b);
}

float blinnPhongBRDF( vec4 n, vec4 v, vec4 lightDirection) {
    vec4 H = halfVector(v, lightDirection);

    float cosTheta = dot(n, v);
    float frs = fresnel(cosTheta);

    return frs * pow(max(dot(n, H), 0), shininess) * lightIntensity;
}

vec4 blinnPhongSpecular(vec4 color, vec4 n, vec4 v, vec4 lightDirection)
{ 
    return color * blinnPhongBRDF(n, v, lightDirection);
}

vec4 blinnPhongColor(vec4 color, vec4 p, vec4 n, vec4 v)
{
    vec4 lightDirection = lightDirection(p);
    
    vec4 Ca = blinnPhongAmbient(color);
    vec4 Cd = blinnPhongDiffuse(color, n, lightDirection);
    vec4 Cs = blinnPhongSpecular(color, n, v, lightDirection);

    return Ca + Cd + Cs;
}

float G1(float cosTheta)
{
    float tanThetaSqrd = 1 / pow(cosTheta, 2) - 1;
    float alpha = shininess / 400;

    return 2 / (1 + sqrt(1 + pow(alpha, 2) * tanThetaSqrd));
}

float chi(float cosTheta) 
{
    float sinTheta = sqrt(1 - pow(cosTheta, 2));
    
    if (cosTheta >= 0 && cosTheta <= 1 && sinTheta >= 0 && sinTheta <= 1)
        return 1.0;
    else
        return 0.0;
}

float chi2(float cosTheta) 
{
    float cosThetaSqrd = pow(cosTheta, 2);
    float sinThetaSqrd = 1 - cosThetaSqrd;

    if (cosThetaSqrd >= 0 && cosThetaSqrd <= 1 &&
        sinThetaSqrd >= 0 && sinThetaSqrd <= 1) 
        return 1.0;
    else
        return 0.0;
}

float D(float cosTheta) 
{
    float tanThetaSqrd = 1 / pow(cosTheta, 2) - 1;
    float alphaSqrd = pow(shininess / 200, 2);
    float P22 = M_1_PI * alphaSqrd / (pow(alphaSqrd + tanThetaSqrd, 2));

    return (P22 * chi(cosTheta)) / pow(cosTheta, 4);
}

vec4 cookTorranceSpecular(vec4 color, vec4 p, vec4 n, vec4 v, vec4 lightDirection)
{
    vec4 i = lightDirection;
    vec4 o = v;
    vec4 h = halfVector(i, o);

    float cosThetaI = dot(i, n);
    float cosThetaO = dot(o, n);
    float cosThetaH = dot(h, n);
    float cosThetaD = dot(h, i);

    float Gi = G1(cosThetaI);
    float Go = G1(cosThetaO);

    float fresnel_d = fresnel(cosThetaD);

    float Dh = D(cosThetaH);

    return color * fresnel_d * Dh * Gi * Go / (4 * cosThetaI * cosThetaO);
}

vec4 cookTorranceColor(vec4 color, vec4 p, vec4 n, vec4 v)
{
    vec4 lightDirection = lightDirection(p);

    vec4 Ca = blinnPhongAmbient(color);
    vec4 Cd = blinnPhongDiffuse(color, n, lightDirection);
    vec4 Cs = cookTorranceSpecular(blinnPhongSpecular(color, n, v, lightDirection), p, n, v, lightDirection);

    return Ca + Cd + Cs;
}

vec4 directIllumination(vec4 color, vec4 p, vec4 n, vec4 v)
{
	// Goal: compute direct illumination at point p, from the light source at lightPosition.
	// color: local material color, RGBA
	// p: coordinates of intersection point, xyzw
	// n: normal at intersection point, xyzw
	// v: incoming viewing direction. Points towards the eye.
  //  if (blinnPhong) 
  //      return blinnPhongColor(color, p, n, v);
  //  else 
  //      return cookTorranceColor(color, p, n, v);


    // TODO figure out how adjust point randomly  
    // vec4 adjustedPoint = p + 0.01;
    bool isInShadow = inShadow(p, n);

    if (isInShadow) {
        return blinnPhongAmbient(color);
    } else {
        //  full lighting model
      if (blinnPhong) 
          return blinnPhongColor(color, p, n, v);
      else 
          return cookTorranceColor(color, p, n, v);
    }
}

const int MAX_TRACE = 5;

void getLocal(vec4 origin, vec4 dir, hitinfo_t h, out vec4 color, out vec4 p, out vec4 n)
{
    p = origin + h.t.x * dir;
    if (h.hit_vptr == GROUND_VPTR) {
        color = texture(colorTexture, h.t.pq);
        p.y = -groundDistance;
        n = vec4(0.0, 1.0, 0.0, 0.0);
    } else {
        color = interpolateColor(h);
        n = interpolateNormal(h);
    }
}


struct trace_state_t
{
    vec4 origin;  // start of ray
    vec4 dir;     // direction
    vec4 color;   // color after multiple bounces
    vec4 localColor;   // color of the material (after interpolation)
    hitinfo_t h;  // what the ray hits (triangle Id, u, v, lambda)
    vec4 hit;     // point the ray hits 
    vec4 Ld;      // direction to light at destination
    vec4 N;       // normal at destination
};

vec4 trace(vec4 origin, vec4 dir)
{
    // These variables are for TP2
    trace_state_t stack[MAX_TRACE + 1];
    bool fallback[MAX_TRACE + 1];
    int sp = 0;

    // TODO 2) Forward tracing: for each ray, compute the intersection, 
    // store the intersection, compute the bouncing ray, 
    // iterate until the ray does not intersect anything or until you have reached the maximum number of bounces. 

    // corner case
    stack[0].origin = origin;
    stack[0].dir = dir;

    // int last_bounce = 0;

    // for (int i = 0; i < MAX_TRACE; i++)
    while (sp < MAX_TRACE)
    {
        hitinfo_t hl;
        bool intersection = firstIntersection(stack[sp].origin, stack[sp].dir, stack[sp].h);

        // shall we compute this before breaking the loop otherwise ?
        getLocal(stack[sp].origin, stack[sp].dir, stack[sp].h, stack[sp].localColor, stack[sp].hit, stack[sp].N);

        stack[sp].Ld = lightDirection(stack[sp].hit);

        // ray bounces into infinity ?
        if (!intersection) { 
            break;
        }

        stack[sp + 1].origin = stack[sp].hit;

        // 100% mirror
        stack[sp + 1].dir = reflect(stack[sp].dir, stack[sp].N);
        sp++;   
    }

    //TODO 3) Backward color computing: you can now compute the ray color. 
    // Start by computing the color for the last point hit (the one further from the eye),
    // using direct illumination and a shadow ray, then move down the stack.
    // At each point, you have to compute the direct lighting using a shadow ray,
    // and combine it with indirect lighting (what you have computed so far). 

   // going to void
    if (sp == 0) {
        return vec4(0, 0, 0, 1);
    }

    stack[sp].color = directIllumination(stack[sp].localColor, 
                                        stack[sp].hit,
                                        stack[sp].N, -stack[sp].dir);

    float reflectivity = 0.6;

    for (int i = sp - 1; i >= 0; i--)
    {
        vec4 direct = directIllumination(stack[i].localColor, stack[i].hit, stack[i].N, -stack[i].dir);

        // stack[i].color = stack[i].localColor * stack[i + 1].color + direct;

        stack[i].color = stack[i].localColor * (1 - reflectivity) * stack[i + 1].color + reflectivity * direct;
    }

    return stack[0].color;
}

layout (local_size_x = 8, local_size_y = 8) in;
void main(void) {
	// Convert local screen coordinates into world coordinates
    ivec2 pix = ivec2(gl_GlobalInvocationID.xy);

    int halton_index = (pix.x + pix.y + framecount) % 256;

    // vec2 noised_pix = pix + gold_noise(pix, framecount) - 0.5;
    vec2 noised_pix = pix + halton[halton_index] - 0.5;

    ivec2 size = imageSize(framebuffer);
    if (pix.x >= size.x || pix.y >= size.y) {
        return;
    }

    vec2 pos = noised_pix / (size - vec2(0.5, 0.5));

    // pos in [0,1]^2 Need it in [-1,1]^2:
    pos = 2 * pos - vec2(1., 1.);

    // Step 1: I need pixel coordinates.i
    vec4 worldPos = vec4(pos.x, pos.y, 1.0, 1.0);
    worldPos = persp_inverse * worldPos;
    worldPos /= worldPos.w;
    worldPos.w = 0;
    worldPos = normalize(worldPos);
    
    // Step 2: ray direction:
    vec4 dir = normalize((mat_inverse * worldPos));
    vec4 eye = (mat_inverse * vec4(0, 0, 0, 1));


    vec4 oldColor = imageLoad(framebuffer, pix);
    vec4 oldSqAvg = imageLoad(sqAvgBuffer, pix);

    // Check variance, return with the old "converged" color if low
    vec4 variance = oldSqAvg - oldColor * oldColor;
    vec4 stdDev = sqrt(max(variance, 0.0));

    if (framecount > 10 && max(stdDev.x, max(stdDev.y, stdDev.z)) < 0.05 ) {
        imageStore(framebuffer, pix, oldColor);
        return;
    }

    vec4 color = trace(eye, dir);

    vec4 newColor = (framecount * oldColor + color) / float(framecount + 1);
    vec4 newSqAvg = (framecount * oldSqAvg + color * color) / float(framecount + 1);

    imageStore(framebuffer, pix, newColor);
    imageStore(sqAvgBuffer, pix, newSqAvg);
}